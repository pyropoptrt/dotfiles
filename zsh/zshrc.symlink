# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
# export PROJECTS=~/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
function certs_expiry {
	echo | openssl s_client -servername $@ -connect $@:443 2>/dev/null | openssl x509 -noout -issuer -subject -dates;
}

alias nmap_ssl="$(which nmap) -sV -sC --script ssl-cert"
# Convert p7b keychains to the PEM format
function pem {
	local newfile="${@:r}-full.pem"
	local oldfile="${@}"

	# prompt for our server name so we can automatically upload our file
	echo "Servername: "
	read servername

	# old commands. saved for prosperity for future troubleshooting
	# openssl pkcs7 -inform PEM -in $@ -outform PEM -out ${@:r}-full.pem -print_certs
	# rm -rf $@

	# convert our file from p7b to pem
	openssl pkcs7 -inform PEM -in ${oldfile} -outform PEM -out ${newfile} -print_certs

	# delete our old cert file
	rm -rf ${oldfile}

	# upload our new cert file
	scp -rpC ${newfile} ${servername}:~

	# Delete our newly created cert
	rm -rf ${newfile}

	# echo created new cert and uploaded
	echo "Created ${newfile} and uploaded to ${servername}"
}
